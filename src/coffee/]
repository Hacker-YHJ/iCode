require '../stylus/main'
domready = require 'domready'
punctuationize = require 'punctuationize'
domtoimage = require 'dom-to-image'

$id = document.getElementById.bind document
$tag = document.getElementsByTagName.bind document
$main = null
$txtYear = null
$txtMonth = null
$txtDay = null
$btnUpload = null
$btnDownload = null
$inputFile = null
$code = null
ratio = 1.413684211
zSpace = '&#8203;'
zNonBreakSpace = '&#8288;'
mainWidth = null
mainHeight = null

domready ->
  $main = $tag('main')[0]
  $txtYear = $id 'txtYear'
  $txtMonth = $id 'txtMonth'
  $txtDay = $id 'txtDay'
  $btnUpload = $id 'btnUpload'
  $btnDownload = $id 'btnDownload'
  $inputFile = $id 'inputFile'
  $code = $tag('code')[0]

  t = new Date()
  $txtYear.textContent = t.getFullYear()
  $txtMonth.textContent = t.getMonth() + 1
  $txtDay.textContent = t.getDate()

  $btnUpload.addEventListener 'click', onUploadClick
  $btnDownload.addEventListener 'click', onDownloadClick
  $inputFile.addEventListener 'change', onInputChange

  window.addEventListener 'resize', onResize
  onResize()

onResize = ->
  if window.innerHeight / window.innerWidth > ratio
    mainHeight = window.innerHeight * ratio * .95
    mainWidth = window.innerWidth * .95
    document.documentElement.style.fontSize = "#{.95 * ratio}vw"
  else
    mainHeight = window.innerHeight * .95
    mainWidth = window.innerWidth / ratio * .95
    document.documentElement.style.fontSize = '0.95vh'
  $main.style.height = "#{mainHeight}px"
  $main.style.width = "#{mainWidth}px"

onUploadClick = ->
  $inputFile.click()

onDownloadClick = ->
  config =
    filter: (node) ->
      !node?.classList?.contains('no-print')
    height: 2 * $main.style.height
    width: 2 * $main.style.width
    style:
      position: 'absolute'
      margin: 0
      height: 2 * $main.style.height
      width: 2 * $main.style.width
      top: '50%'
      left: '50%'
      transform: 'translate(-50%, -50%)'
      webkitFontSmoothing: 'subpixel-antialiased'

  console.log config.height
  console.log config.width

  domtoimage.toPng $main, config
    .then (dataUrl) ->
      link = document.createElement 'a'
      link.download = 'iCode-poster.png'
      link.href = dataUrl
      link.click()
    .catch (err) -> throw err

onInputChange = (input) ->
  file = input.target.files[0]
  return unless file

  reader = new FileReader()
  reader.onload = (e) ->
    contents = e.target.result
    str = punctuationize contents, { space: 'none' }
    while str.length < 100
      str = str + str
    if str.length > 100
      rmd = ~~(Math.random() * (str.length - 100))
      str = str[rmd...rmd + 100]
    nstr = []
    for i in [0...10]
      nstr.push str[i * 10...(i + 1) * 10].split('').join(zNonBreakSpace)
    nstr = nstr.join zSpace
    $code.innerHTML = nstr

  reader.readAsText(file)
